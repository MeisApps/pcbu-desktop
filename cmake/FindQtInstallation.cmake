function(find_qt_installation RESULT_VAR)
    set(${RESULT_VAR} "" PARENT_SCOPE)
    set(_qt_base_dirs)
    if(WIN32)
        list(APPEND _qt_base_dirs "C:/Qt")
    elseif(APPLE)
        list(APPEND _qt_base_dirs "/opt/Qt" "/usr/local/Qt")
    else()
        list(APPEND _qt_base_dirs "/opt/Qt" "/usr/local/Qt")
    endif()

    if(DEFINED ENV{HOME})
        list(APPEND _qt_base_dirs "$ENV{HOME}/Qt")
    endif()
    if(WIN32 AND DEFINED ENV{USERPROFILE})
        list(APPEND _qt_base_dirs "$ENV{USERPROFILE}/Qt")
    endif()
    list(REMOVE_DUPLICATES _qt_base_dirs)

    set(_highest_ver "0.0.0")
    set(_best_path   "")
    foreach(_base ${_qt_base_dirs})
        if(NOT IS_DIRECTORY "${_base}")
            continue()
        endif()
        file(GLOB _candidates LIST_DIRECTORIES true "${_base}/*")
        foreach(_cand ${_candidates})
            if(NOT IS_DIRECTORY "${_cand}")
                continue()
            endif()
            get_filename_component(_ver "${_cand}" NAME)
            if(_ver MATCHES "^[0-9]+\\.[0-9]+")
                if("${_ver}" VERSION_GREATER "${_highest_ver}")
                    set(_highest_ver "${_ver}")
                    set(_best_path   "${_cand}")
                endif()
            endif()
        endforeach()
    endforeach()

    if(_best_path)
        set(${RESULT_VAR} "${_best_path}" PARENT_SCOPE)
        message(STATUS "Qt installation found: ${_best_path}")
    else()
        message(FATAL_ERROR "No Qt installation found. (QT_BASE_DIR)")
    endif()
endfunction()
